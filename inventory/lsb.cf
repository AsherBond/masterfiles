bundle common inventory_lsb
# @brief LSB inventory bundle
#
# This common bundle is for LSB inventory work.
{
  classes:
      "_have_lsb_exec" expression => fileexists($(lsb_exec));
      "_inventory_lsb" and => { "!disable_lsb", "_have_lsb_exec" };

    _inventory_lsb::
      "lsb_$(lsb[Distributor ID])" expression => "any";
      "lsb_$(lsb[Distributor ID])_$(lsb[Release])" expression => "any";

  vars:
      "lsb_exec" string => "/usr/bin/lsb_release";

    _inventory_lsb::
      "lsb[Distributor ID]" string => execresult("$(lsb_exec) -i -s", "noshell");
      "lsb[Release]" string => execresult("$(lsb_exec) -r -s", "noshell");
      "lsb[Description]" string => execresult("$(lsb_exec) -d -s", "noshell");
      "lsb[Codename]" string => execresult("$(lsb_exec) -c -s", "noshell");

      "lsb_keys" slist => getindices("lsb");

      "os" string => canonify("$(lsb[Distributor ID])");
      "release" string => "$(lsb[Release])";
      "flavor" string => canonify("$(lsb[Distributor ID])_$(lsb[Release])");
      "flavour" string => canonify("$(lsb[Distributor ID])_$(lsb[Release])");
      "description" string => "$(lsb[Description])";

  reports:
    inform_mode._inventory_lsb::
      "$(this.bundle): prepared LSB key $(lsb_keys) = '$(lsb[$(lsb_keys)])'";
      "$(this.bundle): OS = $(os), release = $(release), flavor = $(flavor), description = $(description)";
}
